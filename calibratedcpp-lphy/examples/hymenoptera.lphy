data {
    D = readFasta(file="./data/Hymenoptera_100Sites.fasta");
    L = D.nchar(); // length of the alignment
    taxa = D.taxa();
    n = ntaxa(taxa=taxa);
}
model{
    // the calibrations
    // the time is all with unit Ma
    // fossil1: Triassoxyela foveolata - hymenoptera
    tHymenoptera ~ Uniform(lower=226.4, upper=276);

    // fossil2: Triassoxyela foveolata - Ceraphronoidea
    tCeraphronoidea ~ Uniform(lower=130, upper=180);
    taxaCeraphronoidea=["Dendrocerus_carpenteri", "Ceraphron_sp"];

    // fossil3: Rhetinorhyssalus morticinus - Braconidae
    tBraconidae ~ Uniform(lower=99, upper=149);
    taxaBraconidae = ["Cotesia_vestalis","Macrocentrus_marginator","Diaeretus_essigellae","Aphidius_colemani","Aleiodes_testaceus","Dacnusa_sibirica"];

    // fossil4: Protimaspis costalis - Cynipoidea
    tCynipoidea ~ Uniform(lower=78, upper=79);
    taxaCynipoidea = ["Leptopilina_clavipes","balia_leucospoides","Andricus_quercuscalicis","Synergus_umbraculus","Pediaspis_aceris"];

    // fossil5: undescribed species - Chalcidoidea
    tChalcidoidea ~ Uniform(lower=130, upper=180);
    taxaChalcidoidea = ["Nasonia_vitripennis","Torymus_bedeguaris","Brachymeria_minuta","Orasema_simulatrix","Diglyphus_isaea"];

    // fossil6: Lancepyris opertus - Chrysidoidea
    tChrysidoidea ~ Uniform(lower=130, upper=180);
    taxaChrysidoidea = ["Chrysis_viridula","Parnopes_grandior","Elampus_spina","Cleptes_nitidulus","Cephalonomia_tarsalis","Plumarius_gradifrons"];

    // fossil7: Apodolichurus sphaerocephalus - Ampulicidae
    tAmpulicidae ~ Uniform(lower=99, upper=149);
    taxaAmpulicidae = ["Dolichurus_corniculus","Ampulex_fasciata","Ampulex_compressa"];

    // fossil8: Palaeomacropis eocenicus - Melittidae
    tMelittidae ~ Uniform(lower=50, upper=53);
    taxaMelittidae = ["Macropis_fulvipes","Melitta_haemorrhoidalis","Dasypoda_hirtipes"];

    // birth death rate
    diversification ~ Exp(mean=10.0);
    turnover ~ Beta(alpha=2.0, beta=2.0);
    denom = abs(1.0 - turnover);
    lambda = diversification / denom;
    mu = (turnover * diversification) / denom;
    rho ~ Uniform(lower=0.0, upper=1.0);

    // tree prior
    tree ~ CalibratedCPP(cladeAge=[tCeraphronoidea, tBraconidae, tCynipoidea, tChalcidoidea, tChrysidoidea, tAmpulicidae, tMelittidae],
    cladeTaxa=[taxaCeraphronoidea, taxaBraconidae, taxaCynipoidea, taxaChalcidoidea, taxaChrysidoidea, taxaAmpulicidae, taxaMelittidae],
    lambda=lambda, mu=mu, n=n, rho=rho, rootAge=tHymenoptera);

    // GTR substitution model
    π ~ Dirichlet(conc=[3.0, 3.0, 3.0, 3.0]);
    rates ~ Dirichlet(conc=[1.0, 2.0, 1.0, 1.0, 2.0, 1.0]);
    Q = gtr(rates=rates, freq=π);

    // Γ distribution for site rates
    gamma ~ LogNormal(meanlog=0, sdlog=2);
    siteRates ~ DiscretizeGamma(shape=gamma, ncat=4, replicates=L);

    // relax clock log normal model
    sigma ~ LogNormal(meanlog=-0.693, sdlog=0.5);
    branchRates ~ UCLN_Mean1(uclnSigma=sigma, tree=tree);

    // mutation rate
    mRate ~ LogNormal(meanlog=-9, sdlog=5);

    // simulate sequences along the tree, clamp by real data
    D ~ PhyloCTMC(L = L, Q = Q, branchRates = branchRates, mu = mRate, tree = tree, siteRates=siteRates);
}
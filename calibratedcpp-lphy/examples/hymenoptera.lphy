data {
    D = readFasta(file="./data/Hymenoptera_100Sites.fasta");
    L = D.nchar(); // length of the alignment
    taxa = D.taxa();
    n = ntaxa(taxa=taxa);
}
model{
    // the calibrations
    // the time is all with unit 10Mya
    // fossil1: Triassoxyela foveolata - hymenoptera
    allTaxa = D.getTaxaNames();

    // fossil2: Triassoxyela foveolata - Ceraphronoidea
    taxaCeraphronoidea=["Dendrocerus_carpenteri", "Ceraphron_sp"];

    // fossil3: Rhetinorhyssalus morticinus - Braconidae
    taxaBraconidae = ["Cotesia_vestalis","Macrocentrus_marginator","Diaeretus_essigellae","Aphidius_colemani","Aleiodes_testaceus","Dacnusa_sibirica"];

    // fossil4: Protimaspis costalis - Cynipoidea
    taxaCynipoidea = ["Leptopilina_clavipes","Ibalia_leucospoides","Andricus_quercuscalicis","Synergus_umbraculus","Pediaspis_aceris"];

    // fossil5: undescribed species - Chalcidoidea
    taxaChalcidoidea = ["Nasonia_vitripennis","Torymus_bedeguaris","Brachymeria_minuta","Orasema_simulatrix","Diglyphus_isaea"];

    // fossil6: Lancepyris opertus - Chrysidoidea
    taxaChrysidoidea = ["Chrysis_viridula","Parnopes_grandior","Elampus_spina","Cleptes_nitidulus","Cephalonomia_tarsalis","Plumarius_gradifrons"];

    // fossil7: Apodolichurus sphaerocephalus - Ampulicidae
    taxaAmpulicidae = ["Dolichurus_corniculus","Ampulex_fasciata","Ampulex_compressa"];

    // fossil8: Palaeomacropis eocenicus - Melittidae
    taxaMelittidae = ["Macropis_fulvipes","Melitta_haemorrhoidalis","Dasypoda_hirtipes"];

    // birth death rate
    lambda ~ LogNormal(meanlog = 0.1, sdlog = 1);
    mu ~ LogNormal(meanlog = 0.05, sdlog = 1);
    rho ~ Uniform(lower=0.0, upper=1.0);

    upperBounds = [27.6, 18.0, 14.9, 7.9, 18.0, 18.0, 9.9, 5.3] ;
    lowerBounds = [22.64, 13.0, 9.9, 7.8, 13.0, 13.0, 14.9, 5.0];

    // tree prior
    calibrations ~ ConditionedMRCAPrior(calibrationTaxa=[allTaxa,taxaCeraphronoidea, taxaBraconidae, taxaCynipoidea, taxaChalcidoidea, taxaChrysidoidea, taxaAmpulicidae, taxaMelittidae],
    upperBounds=upperBounds, lowerBounds=lowerBounds, rootFlag=true);

    tree ~ CalibratedCPP(calibrations=calibrations, lambda=lambda, mu=mu, n=n, rho=rho);

    // GTR substitution model
    π ~ Dirichlet(conc=[3.0, 3.0, 3.0, 3.0]);
    rates ~ Dirichlet(conc=[1.0, 2.0, 1.0, 1.0, 2.0, 1.0]);
    Q = gtr(rates=rates, freq=π);

    // Γ distribution for site rates
    gamma ~ LogNormal(meanlog=0, sdlog=2);
    siteRates ~ DiscretizeGamma(shape=gamma, ncat=4, replicates=L);

    // relax clock log normal model
    sigma ~ LogNormal(meanlog=-0.693, sdlog=0.5);
    branchRates ~ UCLN_Mean1(uclnSigma=sigma, tree=tree);

    // mutation rate
    mRate ~ LogNormal(meanlog=-9, sdlog=5);

    // simulate sequences along the tree, clamp by real data
    D ~ PhyloCTMC(L = L, Q = Q, branchRates = branchRates, mu = mRate, tree = tree, siteRates=siteRates);
}